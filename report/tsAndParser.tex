\section{Types and Parsing}
\label{sec:types}
In this section, we will describe the data structures we use to represent the the processes, channels and data in our program. We will also briefly cover the way in which we used Parsec to parse our input and create build those structures.

\subsection{Types}
\label{subsec:types}

\subsubsection{Terms}

The most fundamental data type in our implementation is the $Term$. Terms are used to represent data, variables and functions.

\begin{minted}[linenos,frame=lines]{hs}
data Term = TStr String        
          | TNum Integer
          | TBool Bool
          | TBS ByteString
          | TData HttpData
          | TPair (Term, Term)
          | TList [Term]
          | TVar Name (Maybe Type)
          | TFun Name [Term]
            deriving (Eq)
instance Show Term where show = showTerm

type Name = String
\end{minted}
We can see that terms derive Eq, meaning that two terms with the same contents, when compared with $==$ will return $True$. We derive our own instance of Show, which appropriately unwraps each of the above. $Name$ is simple a type synonym for $String$.

TStr, TNum, TBool, and TBS are just simple wrappers for their respective types (String, Integer, Bool and ByteString), to allow us to manipulate them together in Haskell's type system.

TData is a wrapper for HttpData, which is explained in \ref{para:httpdata}

The next few are slightly more complicated. TPair is a wrapper around Haskell's own Tuple type, which can recursively hold two Terms. TList is a wrapper around Haskell's List type, which holds a list of Terms.

TVar is used as a variable. It is constructed using a Name (String) and a Maybe Type. Maybe is a data type in the Haskell Prelude that allows either for a value (using the constructor Just, e.g. Just 1) or no value (using the Nothing constructor). This means that we allow a variable to have no type, but it is possible to construct a variable with a type. Type itself only has two constructors: 

\paragraph{Type}
\begin{minted}[linenos,frame=lines]{hs}
data Type = HttpRequest
          | HttpResponse
          deriving (Eq, Read, Show)
\end{minted}
These are currently only used when receiving data in from an external channel, which is explained in section \ref{sec:channels}

Finally, a TFun is constructed with a Name and a list of Terms. They represent a function (either over terms or over processes).\footnote{It would have been possible to do away with both TPair and TList, and simply had a special case of TFun (which is in fact how they are constructed in the Parser) but we found that this quickly became tedious, so we created these special types which made manipulating them easier.}

\paragraph{HttpData}
\label{para:httpdata}

HttpData is itself a wrapper around the $Response$ and $Request$ type constructors from the Network.HTTP.Base \cite{hack:httpBase} module. Again, we began our implementation with these being special cases of TFun, however we found ourselves converting to and from Responses and Requests that we cut out the middle TFun and added them to our Term structure.
\begin{minted}[linenos,frame=lines]{hs}
data HttpData = Resp (Response String)
              | Req  (Request String)

instance Show HttpData where show = showHttpData
instance Eq HttpData where (==) = eqHttpData
\end{minted}

We created our own instances of Show and Eq in order to unwrap the responses/requests from our constructors. 

We also had to unwrap responses and requests from our constructors when creating an instance of HasHeaders \cite{hack:headers} which is a typeclass used in many header manipulation functions of the HTTP module
\begin{minted}[linenos,frame=lines]{hs}
instance HasHeaders HttpData where
    getHeaders (Resp r) = getHeaders r
    getHeaders (Req r)  = getHeaders r
    setHeaders (Resp r) = Resp . setHeaders r
    setHeaders (Req r)  = Req . setHeaders r
\end{minted}
\subsubsection{Processes}

Processes are modelled with the $PiProcess$ data type.\footnote{N.B. This is merely how processes are modelled, not how they are actually implemented, this is covered in section \ref{sec:processes}}

\begin{minted}[linenos,frame=lines]{hs}
data PiProcess = Null
               | In   Term Term
               | Out  Term Term
               | New  Term
               | PiProcess `Seq` PiProcess 
               | Conc [PiProcess]          
               | Replicate PiProcess       
               | Let Term Value (Maybe PiProcess)
               | If Condition PiProcess PiProcess
               | Atom Term
                 deriving (Eq)

instance Show PiProcess where show = showPi
\end{minted}
This data type derives Eq and we defined our own Show instance (the output of which is identical to our defined syntax for the language). 

The constructors are described as follows:

\begin{description}
    \item[$Null$] Represents the null, or terminal process
    \item[$In$] Represents a process receiving data in on a channel. The first term should be either a TFun which evaluates to a channel, or a variable pointing to one, and the second should be a variable
    \item[$Out$] Represents a process sending data out on a channel. The first term should evaluate to a channel, as in In, and the second can be a term of any type
    \item[$New$] Represents the reservation of a variable name 
    \item[$Seq$] Represents the sequential composition of two processes
    \item[$Conc$] Represents the concurrent composition of a list of processes
    \item[$Replicate$] Represents the infinite concurrent composition of a process with itself
    \item[$Let$] Represents the assignment of a variable, or function to a value or function body. The Maybe PiProcess here represents the possibility either of locally assigning the variable in the process found within the Just constructor, or globally assigning the variable if the Maybe is Nothing.
    \item[$If$] Represents the conditional execution of either the first process if the Condition is true, or the second process if it is false. Conditions are explained in section \ref{para:condition}
    \item[$Atom$] Represents a reference to a process, either as a TFun or a TVar.\footnote{These must evaluate to a PiProcess, or else an error will occur}
\end{description}

The Atom constructor was added to the language in order to allow calling process functions. We encountered a problem when we were implementing the language in that we had the ability to define process functions and references to processes using let, but had no way of calling them within our PiProcess type at the time. Our solution was to add this constructor, which is used by prepending "\&" to a bare Term. See section \ref{eg:recursivecall} for an example of its use.
\paragraph{Condition}
\label{para:condition}

Conditions are modelled with a very simple data type. They currently only allow a single constructor:

\begin{minted}[linenos,frame=lines]{hs}
data Condition = Term `Equals` Term deriving (Eq)

instance Show Condition where show = showCond
\end{minted}

We had thought that we would need to extend this, however we found that no other conditional types were needed. There is still the possibility to provide this extension later.

\subsubsection{Channels}

A Channel is modelled as follows:

\begin{minted}[linenos,frame=lines]{hs}
data Channel = Channel {
               send         :: String -> IO ()
             , receive      :: IO String
             , extra        :: [String]
             }
\end{minted}

We have a single constructor, Channel, which takes two functions and a list of strings:
\begin{description}
    \item[$send$] Any function which takes a string and returns IO (). It is of course intended that this sends this string to a destination, although what this destination is and how this is achieved is not specified here.
    \item[$receive$] Any function which produces a String in the IO monad. Again, this function is intended to retrieve this string from some source, but once again this is not specified here.
    \item[$extra$] Contains any extra data that might be associated with the channel. This is used in the serialisation of Channels which we explain in more detail in section \ref{sec:serialisingChannels}.
\end{description}

The actual implementation of a Channel is left deliberately vague, to allow for many different types of channels to be built. In section \ref{sec:channels} we give our implementations.

\subsubsection{Values}
\label{sec:values}
Values are the data type we use to represent all of our types in the single environment we pass around our evaluation strategy (see section \ref{sec:main})

\begin{minted}[linenos,frame=lines]{hs}
data Value = Proc PiProcess 
           | Term Term
           | Chan Channel
           | PrimitiveFunc TermFun
           | Func {params :: [String] , body :: Value, closure :: Env}

instance Show Value where show = showVal
instance Eq Value where (==) = eqvVal
\end{minted}

The first three are simply wrappers around the data types we have described so far.
PrimitiveFunc models a primitive Term function, and contains a TermFun, defined in section \ref{sec:termfun}
Func models a user defined function, which holds a list of parameters (as a list of Strings), a function body (itself a Value), and a closure within which the function is evaluated (as an Env, the type of which is described in section \ref{sec:environment}). The implementation of these two will be explained in section \ref{sec:main}

We define our own instances of Show and Eq

\begin{minted}[linenos,frame=lines]{hs}
showValue :: Value -> String
showValue (Proc p)  = show p
showValue (Term t)  = show t
showValue (Chan c)  = show (convert c)
    where 
        convert ch = TFun "<chan>" (map TStr ex) 
            where ex = extra ch
showValue (PrimitiveFunc _)  = "<primitive>" 
showValue (Func {})          = "<user function>"  

eqvVal :: Value -> Value -> Bool
eqvVal (Proc p1)  (Proc p2) = p1 == p2
eqvVal (Term t1)  (Term t2) = t1 == t2
eqvVal _ _ = False
\end{minted}

Our show implementation is uninteresting, besides for showing Channels. This is essentially a hack for serialising channels\footnote{When we receive a term in a phi process, we check to see whether it has the special name "<chan>" and then try to build a channel from the data in the arguments, we go into this in more detail in section\ref{sec:serialisingChannels}}

\subsubsection{Environment}
\label{sec:environment}


\begin{minted}[linenos,frame=lines]{hs}
type Env = IORef (Map String Value)
\end{minted}

We model our environment as an IO Reference to a Map of Strings to Values. An IO Reference, or IORef \cite{hack:ioref} can be thought of as a pointer to some mutable state in the IO monad. Our environment will obviously change through our implementation, and so we need to model this mutability. We chose IORefs over other possible mutability models because they are fast, and we are already operating within the IO monad for a lot of our evaluation strategy.

\subsubsection{Error Handling}

Finally, we have our data type representing errors in our evaluation and/or parsing.

\begin{minted}[linenos,frame=lines]{hs}
data PiError = NumArgs Name Integer [Value]
             | TypeMismatch String [Value]
             | Parser ParseError
             | UnboundVar String String
             | NotTerm Name Value
             | NotFunction String String
             | NotChannel String
             | NotProcess String
             | PatternMatch Term Term
             | Default String

instance Show PiError where show = showError
\end{minted}

The purpose of these errors is fairly clear from the names of their constructors.

We represent functions which can throw errors in two ways. They can have two return types:

\begin{description}
    \item[ThrowsError a] meaning they return either a PiError (which is raised using throwError from the Control.Monad.Error module \cite{hack:monadError}) or return something of type a (using return, which lifts a value into the Either monad \cite{hack:either})
    \item[IOThrowsError a] meaning they can return a PiError or a something of type a, but can also perform operations in the IO monad, internally using the ExceptT monad \cite{hack:monadExcept} which is an instance of the MonadIO typeclass \cite{hack:monadIO}.
\end{description}

The type synonyms for these are as follows

\begin{minted}[linenos,frame=lines]{hs}
type ThrowsError a   = Either  PiError a
type IOThrowsError a = ExceptT PiError IO a
\end{minted}

We also have a type synonym for TermFun s

\label{sec:termfun}
\begin{minted}[linenos,frame=lines]{hs}
type TermFun = [Term] -> ThrowsError Term
\end{minted}

So a TermFun is a function which takes a list of Terms and can either throw an error or return a Term

\subsection{Parser}
\label{subsec:parser}

As mentioned in section \ref{sec:bgparsing} we have used Parsec in order to build our parser. The Parser module exports the following functions:

\begin{minted}{hs}
readTerm :: String -> ThrowsError Term 
readProcess :: String -> ThrowsError PiProcess
readProcesses :: String -> ThrowsError [PiProcess]
\end{minted}

All three of which take a String and they return a Term, a PiProcess or a list or PiProcesses respectively in the ThrowsError monad. 

However, these are just wrappers around the two workhorses of this module, which are
\begin{minted}{hs}
parseTerm :: Parser Term
\end{minted}
and 
\begin{minted}{hs}
parseProcess :: Parser PiProcess
\end{minted}

\subsubsection{parseTerm}
As we have already said, when building parsers in Parsec, we construct a larger parser from a set of smaller parsers.
The body of parseTerm is as follows:
\begin{minted}[linenos,frame=lines]{hs}
parseTerm =  try parseAnonChan
         <|> try parseTFun
         <|> parseTVar
         <|> parseTNum
         <|> parseTStr
         where
            parseAnonChan = do
                _ <- char '{'
                spaces
                arg <- many parseTerm
                spaces
                _ <- char '}'
                return (TFun "anonChan" arg)
\end{minted}

So when we parse a term, we first try to parse an anonymous channel, which is represented as a TFun (more on this in section \ref{sec:channels}) and then a general TFun, then a TNum, then a TVar, and finally a TStr.
The final two are somewhat uninteresting,  but the parseTFun and parseTVar functions contain a couple of special cases.

\begin{minted}[linenos,frame=lines]{hs}
parseTFun :: Parser Term
parseTFun = do
            name <- readVar
            spaces
            args <- bracketed $ sepBy parseTerm paddedComma
            return $ case (name,args) of
                ("pair", t1:t2:_)  -> TPair (t1,t2)
                ("list", _ )       -> TList args
                _                  -> TFun  name args 
\end{minted}

In most cases this parser will return a TFun, but in the case that the name of the TFun we are parsing is "pair" we return a TPair, and in the case that it is "list" we return a TList.

\begin{minted}[linenos,frame=lines]{hs}
parseTVar :: Parser Term
parseTVar = do
        v <- readVar
        case v of
            "true"  -> return $ TBool True
            "false" -> return $ TBool False
            _       -> do
                t <- myOptionMaybe parseType (paddedChar ':') 
                return (TVar v t)
\end{minted}

This parser has an "escape clause" for TBools. When the variable we have parsed is "true" or "false" we return the appropriate TBool. In the case that it is neither, we then attempt to parse the Type of the variable when there may or may not be one. We do this using the myOptionMaybe parser, which has the type:

\begin{minted}{hs}
myOptionMaybe :: Show b => Parser a -> Parser b -> Parser (Maybe a)
\end{minted}

So given a parser of some type a, and a parser of something b which is an instance of Show, we return a parser of type Maybe a. Here we pass myOptionMaybe a Parser Type and a Parser Char (Char being an instance of Show) and this will give us a Parser Maybe Type, with Maybe Type being the type of the second argument to the TVar constructor.

myOptionMaybe is in fact just a specialist call to a more general function myOption
\begin{minted}{hs}
myOptionMaybe parser = myOption Nothing (liftM Just parser)
\end{minted}

myOption being defined as:

\begin{minted}{hs}
myOption :: Show b => a -> Parser a -> Parser b -> Parser a
myOption opt parser sep = try (notFollowedBy sep >> return opt) <|> (sep >> parser)
\end{minted}

This function is used several times throughout parseTerm and parseProcess. When given a parser for a separator, we check if the separator can be parsed using notFollowedBy. If not then we return the first argument to myOption, opt (of type a), however, if it can be parsed (i.e. notFollowedBy fails) then we parse it, ignoring its result, and then use the parser "parser" to continue parsing the rest of the input.

There is in fact a function option (and associated optionMaybe) in the Parsec library, which does nearly the same thing. However, they consume input even if the sep parser fails, which was causing havoc with our implementation, and so we wrote our own version.

\subsubsection{parseTerm}
\begin{minted}[linenos,frame=lines]{hs}
parseProcess :: Parser PiProcess
parseProcess = liftM (\ps -> case ps of 
                        [p] -> p
                        _   -> Conc ps) (sepBy1 parseProcess' (char '|'))
    where
    parseProcess'  = bracketed parseProcess'' <|> parseProcess''
    parseProcess'' = parseNull 
                 <|> try parseIf
                 <|> parseIn 
                 <|> parseOut
                 <|> parseLet
                 <|> parseReplicate
                 <|> parseNew
                 <|> parseAtom
\end{minted}

This function is best looked at backwards. From line 7, we have the main bulk of the parser, i.e. the combination of the smaller parsers. In line 6 we see a bit of fidgeting for the parsing of bracketed processes, and in line 4 we see that we separate these (potentially bracketed) processes with a | character. The lambda function in lines 2-4 simply avoids unnecessary nesting of singleton lists in Conc constructors. 

Of note here is the lack of a parser for sequentially composed processes. The reason for this is that only a subset of processes can actually be composed sequentially. That is to say, In,Out,New, and Atom. As such parseSeq is only called within the parsers for these four. Also of note with regards to the parseSeq parser is that any process that can be sequentially composed with another, can very well be composed with the Null process, which can either be written explicitly
or through omission of a proceeding semicolon and process. This is achieved using the myOption parser we have previously mentioned:

\begin{minted}{hs}
parseSeq :: PiProcess -> Parser PiProcess
parseSeq p1 = do
            p2 <- myOption Null parseProcess (paddedChar ';')
            return (p1 `Seq` p2)
\end{minted}

The final subsection of the parser we will discuss is parseLet.

\begin{minted}[linenos,frame=lines]{hs}
parseLet :: Parser PiProcess
parseLet = do
            _ <- string "let"
            spaces
            name <- parseTerm
            paddedChar1 '='
            val <- try (liftM Proc parseProcess) <|> liftM Term parseTerm
            p <- myOptionMaybe parseProcess (paddedStr1 "in") 
            return (Let name val p)
\end{minted}

This is interesting because not only do we have the potential for a let clause without an "in" which is handled by myOptionMaybe, but we also have the possibility of either parsing a process or a term for the value which is being assigned to the variable. Because of this we must use either the Proc or Term constructors to turn the PiProcess or Term into a Value\footnote{\ref{sec:values}}

The full code for our parser implementation can be found in appendix \ref{sec:parserimplementation}
