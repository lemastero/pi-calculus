\section{Evaluation Strategy and Bringing it All Together}
\label{sec:main}
\subsection{Evaluation Strategy Influence}
Having never written an interpreter before, we did some research as to how one might go about doing so. We quickly found Jonathan Tang's Wikibook "Write Yourself a Scheme" \cite{wyas}, which breaks down the construction of an interpreter for the Scheme Lisp dialect. While obviously not entirely translatable to our own project, we undertook this particular tutorial, and found the exercise a very helpful basis for creating an interpreter of our own.

\subsection{Main module}
The main module of our program is the PiCalculus module. This module essentially acts to join together the previously mentioned modules and adds some further functionality. 
There are three main functions of this module
\begin{enumerate}
    \item Handling user input and output
    \item Evaluation of our Data structures including
        \begin{enumerate}
            \item Handling the Environment, which is to say variable assignment
            \item Handling function application
            \item Performing pattern matching
        \end{enumerate}
    \item Handling information received from Channels appropriately
\end{enumerate}

\subsection{User Input and Output}

In this section we describe how input from the user is brought into our evaluation strategy, and then information is handed back to the user. Many of the ideas here are inspired by \cite{wyas}.

\subsubsection{Running the program}
\begin{minted}[linenos,frame=lines]{hs}
main :: IO ()
main = do
        name   <- getProgName
        args   <- getArgs
        case args of
            []  -> runRepl coreBindings
            [x] -> readFile x >>= runProcess coreBindings 
            _   -> do
                    putStrLn           "Use:"
                    putStrLn $ name ++ " -- Enter the REPL"
                    putStrLn $ name ++ " [process] -- Run single process"
\end{minted}

Our main function, which is called every time we type "phi" at the command line. When given one argument, we get the contents of the file with that name and then call runProcess on the returned contents.

\begin{minted}[linenos,frame=lines]{hs}
runProcess :: IO Env -> String -> IO ()
runProcess core expr = core >>= flip evalAndPrint expr
\end{minted}

runProcess accepts a set of core bindings and then calls eval and print on the string in its second argument

\begin{minted}[linenos,frame=lines]{hs}
evalAndPrint :: Env -> String -> IO ()
evalAndPrint env expr = do
            res <- evalString env expr 
            case res of
                "()"  -> return ()
                _     -> putStrLn res
\end{minted}

evalAndPrint evaluates a string in a given environment. The case clause here is to avoid "()" being printed every time we evaluate a string to Void\footnote{()}

\begin{minted}[linenos,frame=lines]{hs}
evalString :: Env -> String -> IO String
evalString env expr = 
        runIOThrows $
        liftM show $ 
        liftThrows (readProcess expr) >>= eval env
\end{minted}
evalString is best understood if we break it down line by line. In line 5,  liftThrows lifts its argument from ThrowsError into IOThrowsError, so the result of readProcess expr (so the result of parsing the string we pass into readProcess) is lifted. This lifted value is then passed into eval env where it is evaluated in the given environment using eval \ref{sec:eval}.
Once this has been evaluated, we map show over the result of evaluating our parsed expression, and then call runIOThrows on that result, meaning we are now calling runIOThrows on a value of type IOThrowsError String.

\begin{minted}[linenos,frame=lines]{hs}
runIOThrows :: IOThrowsError String -> IO String
runIOThrows action = liftM extractValue 
                     (runExceptT (trapError action))

trapError :: IOThrowsError String -> IOThrowsError String
trapError action = catchE action (return . show)
\end{minted}

runIOThrows first calls trapError on the action passed in, which catches an exception thrown in the action and returns it.Then we call runExceptT which has type

\begin{minted}{hs}
runExceptT :: e m a -> m (Either e a)
\end{minted}

which for us means

\begin{minted}{hs}
runExceptT :: PiError IO String -> IO (Either PiError String)
\end{minted}

Then we lift a call to extractValue to retrieve the value from the Either PiError String
However, because we have already caught any potential exceptions and returned them in the IOThrowsError monad, we will never encounter a Left in this Either, so we are safe to put a call to error in this function. 

\begin{minted}[linenos,frame=lines]{hs}
extractValue :: ThrowsError a -> a 
extractValue (Right v) = v
extractValue (Left  e) = error (show e)
\end{minted}

So, backtracking, this now gives us a value of type IO String, which is exactly what we want. 

If however we are passed no arguments, we enter the Read-Eval-Print Loop, where the only function we have not already met is until_, which simply keeps performing a monadic action until(!) a condition is met

\begin{minted}[linenos,frame=lines]{hs}
runRepl :: IO Env -> IO ()
runRepl core = core >>= until_ quit 
               (readPrompt "phi>") . evalAndPrint
        where
            quit = flip any [":quit",":q"] . (==)
\end{minted}

\subsection{Evaluation of Data Structures}
\label{sec:eval}

The two main functions in evaluation are

\begin{minted}[linenos,frame=lines]{hs}
eval :: Env -> PiProcess -> IOThrowsError () 
evalTerm :: Env -> Term -> IOThrowsError Value
\end{minted}

each of which gets passed the Environment IORef at each call, and a PiProcess, Term, or Condition respectively.

\subsubsection{Environment}

There are three functions for manipulating the environment
\begin{description}
    \item[getVar] reads the environment IORef, then looks up the given variable name in the environment map, throwing an error if it does not exist.
    \item[defineVar] reads the environment IORef, then inserts the value into the map with the given name. If the name already exists it is overwritten. We then write the new environment to the original IORef
    \item[bindVars] reads the environment IORef, then creates a union between the current environment bindings and those in the associative list passed in. Instead of writing over the original IORef, we create a new one which is returned from this function. This allows us to create function closures, and "let..in.." bindings easily.
\end{description}
\begin{minted}[linenos,frame=lines]{hs}
getVar :: Env -> String -> IOThrowsError Value 
getVar envRef var = do 
        env <- liftIO $ readIORef envRef
        maybe (throwE $ UnboundVar "Getting an unbound variable" var)
              return
              (Map.lookup var env)
defineVar :: Env -> String -> Value -> IOThrowsError ()
defineVar envRef var val = liftIO $ do
         env      <- readIORef envRef
         writeIORef envRef $ Map.insert var val env
bindVars :: Env -> [(String , Value)] -> IO Env
bindVars envRef bindings = do
                env <- readIORef envRef
                newIORef (Map.union (Map.fromList bindings) env)
\end{minted}


\subsubsection{eval}

eval is the function which recurses through the whole structure of our processes, evaluating as it goes along. Here we go into the most interesting cases:


\begin{minted}[frame=lines]{hs}
eval _ Null = return ()
\end{minted}
Evaluating Null does nothing, as we would hope.

\begin{minted}[linenos,frame=lines]{hs}
eval env (In a v@(TVar b t)) = do
    chan <- evalChan env a
    term <- receiveIn chan t
    bindings <- case term of
        TFun "<chan>" ex -> do
                ch <- decodeChannel ex
                return [(b,ch)]
        _ -> liftThrows $ match v term
    mapM_ (uncurry (defineVar env)) bindings
    return ()
        where
        decodeChannel e = do
            extraStrings <-  mapM extractString e
            case getChannelData extraStrings of
                Just (h,p)  -> liftM Chan $ liftIO $ newChan Connect h p
                Nothing -> throwE $ Default "incomplete data in channel"
\end{minted}

Receiving a message on a channel involves first evaluating the channel. We then pass the channel and the Maybe Type of the variable to our receiveIn function \ref{sec:receiveIn}. We then have a small section of code for our Channel serialisation hack \ref{sec:serialisingChannels}, and then we attempt to match the original variable against the term we received.\footnote{The keen eyed will notice that this will always return a single binding, as we are passing a TVar as the first
    argument to match. The reason we have it implemented like this is that if in future we figure out how to match on an in without passing in the type of the data we expect as the type of a variable (see section \ref{sec:receiveIn}) it won't require that much of a reshuffle}
We then monadically map defineVar over our bindings to add them to the environment.

\begin{minted}[linenos,frame=lines]{hs}
eval env (Out a b) = do 
                chan <- evalChan env a
                bVal <- evalTerm env b
                sendOut chan bVal
                return ()
\end{minted}
When we send out a message on a channel, first we evaluate the channel in our environment, and then our message, calling sendOut. We will discuss this more in section \ref{sec:sendout}
\begin{minted}[linenos,frame=lines]{hs}
eval env (Conc procs)  = do
                var <- liftIO newEmptyMVar 
                mapM_ (forkProcess var) procs
                res <- liftIO $ takeMVar var
                case res of
                    Left err -> throwE err
                    Right _  -> return ()
        where
            forkProcess var proc = liftIO $ forkIO $ do
                        res <- runExceptT (eval env proc)
                        _ <- tryPutMVar var res
                        return ()
\end{minted}
When we evaluate a list of processes concurrently, we first of all create an MVar in which we will store the result of the processes. Then we monadically map forkProcess over the list. forkProcess calls forkIO on each element, and then evaluates each process in a new thread, placing its result in the MVar passed in the call to forkProcess. As it stands, the MVar only contains the result of the first process to finish executing, so if an error were to occure in a longer running process, it
would not raise an exception. This is something we would address in future. 

\begin{minted}[frame=lines]{hs}
eval env (Replicate proc) = 
    liftIO (threadDelay 100000) >> eval env (Conc [proc, Replicate proc])
\end{minted}
Replicated processes are evaluated using Conc. There is a delay added because without it we quickly run out of memory. GHC threads may be lightweight, but nothing lightweight enough to spawn ad infinitum.

\begin{minted}[linenos,frame=lines]{hs}
eval env (p1 `Seq` p2) = do
                eval env p1
                eval env p2
\end{minted}
Evaluating two processes sequentially is handled as one would expect. We evaluate the first and then the second.

New and If are handled exactly as one would imagine. New simply reserves a name in the Environment, and If evaluates the condition and performs the first process if it evaluates to true and the second process if it evaluates to false.


\paragraph{Function Definition}
Let processes are overloaded with several purposes. We will discuss their use in pattern matching in \ref{sec:letpatterns} One of these is function definition which is achieved using the following three functions:
\begin{minted}[linenos,frame=lines]{hs}
defineGlobalFun :: Env -> String -> [Term] -> 
                    Value -> IOThrowsError ()
defineGlobalFun env name args term = 
    defineVar env name $ makeFun args term env

defineLocalFun :: Env -> String -> [Term] -> 
                    Value -> PiProcess -> IOThrowsError ()
defineLocalFun env name args term p = do
        clos <- liftIO $ bindVars env [(name, makeFun args term env)]
        eval clos p

makeFun :: [Term] -> Value -> Env -> Value
makeFun args = Func (map show args)
\end{minted}
the first two being wrappers (for defining a function locally to the proceeding process or globally) around the third. Function definition is simply the binding of a list of parameters to a Value and, which could be a PiProcess or a Term, and a closure.

\paragraph{Atoms}
Atoms have a few special cases. The special funcion "load" loads in a file full of let definitions separated by newlines,
The special file "pilude.pi"\footnote{A play on the Haskell Prelude} is available to load at any time when using phi. The special function env can be used to outpute the current values in the environment. In any other case, we evaluate the given term as a process.
\begin{minted}[linenos,frame=lines]{hs}
eval env (Atom (TFun "load" [TStr "pilude.pi"])) = do
    pilude <- liftIO $ getDataFileName "pilude.pi"
    eval env (Atom (TFun "load" [TStr pilude]))
eval env (Atom (TFun "load" [TStr file])) = do
    procs <- load file  
    eval env $ foldl Seq Null procs
eval env (Atom (TVar "env" Nothing)) = do
    e <- liftIO $ readIORef env
    liftIO $ mapM_ (\ (k,v) -> putStrLn $ k ++ ": " ++ show v) $ Map.toAscList e
eval env (Atom p@(TFun{})) = void $ evalProcess env p
eval env (Atom p) = do
    proc <- evalProcess env p
    eval env proc
\end{minted}
\subsubsection{evalTerm}

evalTerm is the function we use to evaluate Terms into Values

For TNums, TStrs, TBools, TBSs and TData which are already data in themselves, this is simply a case of passing them to the Term constructor. For other types of Term it is slightly more complicated.
\begin{minted}[frame=lines]{hs}
evalTerm env (TVar name _) = getVar env name
\end{minted}
when we evaluate a bare variable, we retrieve it's value from the environment

\begin{minted}[linenos,frame=lines]{hs}
evalTerm env   (TList ls) = do
    vs <- mapM (evalTerm env) ls
    ts <- extractTerms vs
    return $ Term $ TList ts
evalTerm env (TPair (t1,t2)) = do
    a <- evalTerm env t1
    b <- evalTerm env t2
    case (a,b) of 
        (Term c, Term d) -> return $ Term $ TPair (c,d)
        _                -> throwE (Default "pair not given two terms")
\end{minted}
with lists and pairs we must recursively apply evalTerm to each element to fully evaluate the structure. However because evalTerm returns IOThrowsError Value, we must also check that each item returned into these structures is still a Term. extractTerms does this over a list (throwing an error if it comes across a non-term value), but with pairs we do this manually, as there are only two elements. 

The only remaining kind of term is the TFun. There are a few special cases we cater for, which are the channel building functions, namely:
\begin{minted}[linenos,frame=lines]{hs}
evalTerm env (TFun "anonChan" []) = do
    port <- assignFreePort env
    liftM Chan $ liftIO $ newChan Init "localhost" port 
evalTerm env (TFun "anonChan" [n]) = do
    port <- evalToInt env n
    c <- liftIO $ newChan Init "localhost" port 
    return $ Chan c
\end{minted}
As we saw in section \ref{subsec:parser} an anonChan can be entered by the user using either the full name, or the shortcut syntax (a pair of curly braces, either with a number in between or empty). This is how we create the server end of a channel in phi. An anonChan with a number as an argument\footnote{This is a bit of a misnomer in this instance, but the name stuck around by association with the second usage} creates the server end of a channel at the specified port. An empty anonChan assigns the channel a free port\footnote{This is currently also a "hack". There is a variable in the environment with a name that the parser cannot parse, and which we use to refer to the next free ephemeral
port. Again, currently this starts at the lowest possible ephemeral port ($2^[15]+2^[14]$) and counts upwards, but it would not be too much of a stretch to have it assign a non-allocated one in the range.} This might seem slightly bizarre at first - how is someone supposed to connect to a port whose number one does not know. The included sample program \ref{sec:clientserverfancy} demonstrate how one might use such functionality.
\begin{minted}[linenos,frame=lines]{hs}
evalTerm env (TFun "httpChan" [a]) = do
    host <- evalToString env a
    liftM Chan $ liftIO $ newChan Connect host 80
evalTerm env (TFun "chan" [a,n]) = do
    host <- evalToString env a
    port <- evalToInt env n
    liftM Chan $ liftIO $ newChan Connect host port
\end{minted}
These two functions connect to a channel at a remote location. httpChan connects to port 80 at the given hostname, and chan can connect to any given host on any given port.

The last case of our evalTerm function is:
\begin{minted}[linenos,frame=lines]{hs}
evalTerm env (TFun name args) = do
    fun <- getVar env name
    argVals <- mapM (evalTerm env) args
    apply fun argVals
\end{minted}
which fetches the function from the environment, then monadically maps evalTerm over the arguments, and finally applies the function, which we describe below.

\subsubsection{Function Application}

As we saw in section \ref{sec:values} there are two kinds of function both of which are dealt with in our implementation by
\begin{minted}{hs}
apply :: Value -> [Value] -> IOThrowsError Value 
\end{minted}

In the case of a primitive function, which acts on Terms only, we extract the terms from this list of Values passed in using extractTerms again, and then apply the function\footnote{which is of type $[$Term$]$ $\rightarrow$ ThrowsError Term} , lift its result into the IOThrowsError monad and return it.
\begin{minted}[linenos,frame=lines]{hs}
apply (PrimitiveFunc fun) args = do
        ts <- extractTerms args
        res <- liftThrows $ fun ts
        return $ Term res
\end{minted}

For a user-defined function our task is a little more difficult. We first check if the function has been passed the correct number of arguments, then we zip the list of Values passed in with the list of parameters that Func contains, and bind those values in the closure of the function. If the function body is a Term, then we simply evaluate it in the closure, if it is a Process then we must first evaluate it and then return they body (as eval has a return type of ()) 
\begin{minted}[linenos,frame=lines]{hs}
apply (Func parms bdy closre) args =
    if num parms /= num args 
        then throwE $ NumArgs "user-defined" (num parms) args
        else do
             clos <- liftIO (bindVars closre $ zip parms args)
             case bdy of
                Term t -> evalTerm clos t
                Proc p -> eval clos p >> return bdy
                _      -> throwE (Default "this function makes no sense")
    where
        num = toInteger . length
\end{minted}

\subsubsection{Pattern Matching}
\label{sec:letpatterns}

Here we show how we use our pattern matching utility.

There are two cases here to cater for "let.." (global) and "let..in.." (local) bindings.
\begin{minted}[linenos,frame=lines]{hs}
eval env (Let t1 (Term t2) Nothing) = do 
    val <- evalTerm env t2 
    case val of 
        Term term -> do
            bindings <- liftThrows (match t1 term)
            mapM_ (uncurry (defineVar env)) bindings
            _         -> throwE (Default "Can only pattern match against Terms")
eval env (Let t1 (Term t2) (Just p)) = do
    val <- evalTerm env t2 
    case val of 
        Term term -> do
            bindings <- liftThrows $ match t1 term
            newEnv <- liftIO $ bindVars env bindings
            eval newEnv p
            _         -> throwE (Default "Can only pattern match against Terms")
\end{minted}
In the first case we evaluate the term to the right of the "=", assert that it is still a term, get the bindings from our match function, and monadically map defineVar over the bindings, and in the second case we do exactly the same but call bindVars to create a new local environment.

\subsection{Data from Channels}

The last thing our Main module needs to do is handle data going into and coming out of channels.
\label{sec:sendout}
\begin{minted}[linenos,frame=lines]{hs}
sendOut :: Channel -> Value -> IOThrowsError () 
sendOut chan v@(Chan c) = if serialisable c
                    then liftIO $ send chan $ show v
                    else throwE $ Default "Channel not serialisable" 
sendOut chan val = liftIO $ send chan (show val)
\end{minted}
Sending out on channels is simple. We first check if the thing we are trying to send is a channel, and if it is, if that channel is serialisable. If not, we throw an error, in every other case we simply convert the object into a string using show and then send it out onto the channel.

\label{sec:receiveIn}
\begin{minted}[linenos,frame=lines]{hs}
receiveIn :: Channel -> Maybe Type -> IOThrowsError Term
receiveIn chan t = do
        str <- liftIO $ receive chan
        case t of
            Just HttpRequest  -> makeHttpRequest str
            Just HttpResponse -> makeHttpResponse str
            _                 -> liftThrows $ readTerm str
\end{minted}
Receiving messages onto a channel is more complicated. We first need to check what type of message we are expecting. The reason for this is that the functions for parsing HTTP Requests and Responses are completely different, and so the only way we could think of to ascertain which one to use was to leave it up to the programmer. So, the programmer must pass in the type of the variable we are receiving into. We then match that type against HttpRequest or HttpResponse, calling the appropriate
parsing function, and if it is neither, we assume it is a Term from another instance of phi.

